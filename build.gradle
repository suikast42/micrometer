buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
        mavenLocal()
    }
    dependencies {
        classpath 'org.ow2.asm:asm:5.0.3'
        classpath 'io.spring.gradle:spring-release-plugin:0.19.1'
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.2.0'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath 'com.netflix.nebula:nebula-project-plugin:3.4.0'
        classpath 'io.spring.gradle:dependency-lock:0.2.0'
        classpath "gradle.plugin.com.dorongold.plugins:task-tree:1.3"
    }

    configurations.classpath.resolutionStrategy.cacheDynamicVersionsFor 0, 'minutes'
}

apply plugin: 'io.spring.release'

allprojects {
    apply plugin: 'io.spring.license'
    apply plugin: 'com.dorongold.task-tree'
    apply plugin: 'java'
    apply plugin: 'maven'
    afterEvaluate { project ->

        println "I'm building $project.name with version $project.version"
    }
    group = 'io.micrometer'
    version = "1.1.0.Custom-SNAPSHOT"
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "http://10.83.34.151:8081/nexus/content/repositories/releases/") {
                    authentication(userName: "admin", password: "admin123")
                }

                snapshotRepository(url: "http://10.83.34.151:8081/nexus/content/repositories/snapshots/") {
                    authentication(userName: "admin", password: "admin123")
                }


            }
        }
    }
    
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'spring.lock'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    if(!project.name.contains('samples') && !project.name.contains('benchmarks')) {
        apply plugin: 'io.spring.publishing'

        contacts {
            'jschneider@pivotal.io' {
                moniker 'Jon Schneider'
                github 'jkschneider'
            }
        }

        if (project.extensions.findByName('bintray')) {
            bintray.labels = ['micrometer', 'atlas', 'metrics', 'prometheus', 'spectator', 'influx', 'new-relic', 'signalfx', 'wavefront', 'elastic', 'dynatrace']
        }
    }

    description = 'Application monitoring instrumentation facade'

    repositories {
        mavenCentral()
    }

    checkstyle {
        toolVersion = '8.4'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }

    def check = tasks.findByName('check')
    if(check) project.rootProject.tasks.releaseCheck.dependsOn check

    //noinspection GroovyAssignabilityCheck
    test {
        // set heap size for the test JVM(s)
        maxHeapSize = "1500m"
    }
   
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}

defaultTasks 'build'